\chapter{Introduction}

Given a (possibly non-deterministic) function $f : A \to B$, we say that it is an encryption function iff
\begin{enumerate}
    \itemsep0em
    \item There is a function $g : B \to A$ such that $g \circ f$ is the identity.
    \item Given a $b \in B$, it is hard to find an $a \in A$ such that $f(a) = b$ if $g$ is not known.
\end{enumerate}

The first of these properties is easy to formulate, verify and prove.  The second, on the other hand, is not formal
claim at all.  The goal of this chapter is to show how it can be expressed in objective measures using games,
progressively adding complexity to the system until we can express everything we want to.

In this chapter, we present a simplified system in Haskell which captures many of the computational features we care
about, but in which we cannot express the proofs that we want to reason about.  The rest of this thesis will be
dedicated to formalising both the computations and the proofs involved in Agda, resulting in a library in which these
properties can be proved for further algorithms.

The reader may note that we completely omit discussion of oracles from this chapter.  Oracles are an essential topic,
and their incorporation in the system we construct is one of the main goals for the coming months.  Chapter
\ref{chp:research-plan} contains a brief overview of the progress so far and our plans for the coming months.

\section{Games as Security Levels}

Suppose that we have three parties, Alice, Bob, and Eve.  Alice and Bob have agreed on a key in advance, which Eve does
not know, and are sending each other messages encrypted with that key.  Suppose that Eve intercepts one such message:
what information can she gain based on this?

In order to consider the worst possible case, we will allow Eve to choose two plaintext messages and let the intercepted
message be the ciphertext of one of them.  If Eve cannot tell which message was encrypted even in this scenario, then
she also cannot tell the difference when the message was chosen from some larger set, and we can be certain that Eve
cannot distinguish between the ciphertexts of messages chosen from some larger set.

We can describe the game as follows:
\begin{enumerate}
    \itemsep0em
    \item Alice chooses a key $k$.
    \item Eve chooses two messages, $m_0$ and $m_1$, and gives them to Alice.
    \item Alice flips a coin and gets a result $b \in \{0, 1\}$.
    \item Alice encrypts $m_b$ and gives the result, $m'$, to Eve.
    \item Eve chooses $b' \in \{0, 1\}$ and gives it to Alice.
    \item If $b = b'$ then Eve wins, otherwise Eve loses.
\end{enumerate}

Note that Bob does not figure in this interaction, since his actions don't influence the information Eve has.  As far as
we are concerned, this is a two-player game between the encrypter, Alice, and the eavesdropper, Eve.  The encrypter is
typically called the challenger, while the eavesdropper is called the attacker or the adversary.  We will from now on
describe games from the point of view of the challenger, while treating the adversary as a black box.  The above game
thus becomes:
\begin{enumerate}
    \itemsep0em
    \item Generate a key $k$.
    \item Receive messages $m_0$ and $m_1$ from the adversary.
    \item Flip a coin to get $b \in \{0, 1\}$.
    \item Encrypt $m_b$; call the result $m'$.
    \item Give the adversary $m'$ and get $b'$ back.
    \item Return $b = b'$.
\end{enumerate}

This can, in turn, be regarded as a program in some imperative language, or a computation in some monad.  Computations
performed by the challenger are known, so they can be directly encoded in the program.  Computations performed by the
adversary are given as parameters to the program.  For the moment, we will use Haskell to represent these computations
and assume that there is a monad |Game| that provides some source of randomness and some way for the adversary to
store its state.
\begin{code}
data EncScheme key pt ct = EncScheme
                         { generateKey :: Game key
                         , encrypt :: key -> pt -> Game ct
                         }

data Adversary pt ct = Adversary
                     { chooseMessages :: Game (pt, pt)
                     , chooseOutcome :: ct -> Game Bool
                     }

EAV_game :: EncScheme key pt ct -> Adversary pt ct -> Game Bool
EAV_game enc adv = do
    k <- generateKey enc
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- encrypt enc k (if b then m1 else m0)
    b' <- chooseOutcome adv m'
    return (b == b')
\end{code}

We now have a precise description of the game involved, and we can say that a scheme |enc| is secure against
eavesdropping iff |EAV_game enc adv| is `very close' to |flipCoin| for every choice of |adv|.  We will now give a more
rigorous definition of the |Game| monad in order to formalise this notion of distance.

\section{The |Game| Monad}

In the last section, we showed how games can be seen as terms in some |Game| monad.  This monad represented a
computation that may use some random bits, and in which the adversary may manipulate their state.  This computation can
be interpreted into a concrete value given an initial state and an (infinite) sequence of random bits.

The adversary should be free to choose what type they wish to use for their state, while the challenger should be unable
to in any way access or modify this state.  We can achieve this by parametrising |Adversary| by its state type, while
requiring that our encryption work for any adversary state type.  The example above then becomes
\begin{code}
data EncScheme key pt ct = EncScheme
                         { generateKey :: forall as. Game as key
                         , encrypt :: forall as. key -> pt -> Game as ct
                         }

data Adversary as pt ct = Adversary
                        { chooseMessages :: Game as (pt, pt)
                        , chooseOutcome :: ct -> Game as Bool
                        }

EAV_game :: EncScheme key pt ct -> Adversary as pt ct -> Game as Bool
EAV_game enc adv = do
    ... -- unchanged
\end{code}

Since the challenger has no information about |as|, they cannot perform any operation on it; there is also no way for
|generateKey| to somehow store the state to later use it in |encrypt|.  This correctness-by-parametricity approach has a
downside in Haskell, namely that the challenger could use a nonterminating computation such as |fix id| as a new
Adversary state.  This is not a serious problem for our purposes, since Agda is a total language and thus does not have
this issue.

\subsection{Operations}

There are three classes of operations supported by the |Game| monad: operations involving randomness, which may
be performed by both challenger and adversary, operations manipulating the adversary's state, which may only be
performed by the adversary, and operations manipulating oracle state, which may only be performed by the challenger.
Since we are not yet concerned with oracles, we will only cover the first two categories.

The fundamental operation involving randomness is the flipping of a fair coin.  From this we can build up random
bitstrings of any length.  The resulting operations provided are thus:
\begin{code}
fairCoin :: Game as Bool
uniform :: Int -> Game BitVector
\end{code}

We have so far not provided any way of generating uniform distributions over sets with a size other than a power of two,
as this does not seem to be a common requirement in the domain of cryptography.  Furthermore, not supporting this allows
for an easier interpretation, since the number of random bits necessary per operation is fixed.

In addition to the above, the FCF provides a |repeat| operation which repeats some non-deterministic computation until a
predicate holds on the result.  More research is needed to see whether this is truly necessary for our purposes.  This
would, in particular, allow us to recover uniform distributions over arbitrary finite sets.

The adversary is given access to a state, which acts much like the state of a state monad.  There are corresponding
operations:
\begin{code}
getAdvState :: Game as as
putAdvState :: as -> Game as ()
\end{code}

\subsection{Interpretation}

Now that we have a set of operations, we need to specify their semantics.  We do this by specifying an interpretation of
this monad in a state monad that maintains the adversary state and the random bits.  Since |uniform| can be defined in
terms of |fairCoin|, we only need to provide interpretations forthe |fairCoin|, |getAdvState|, and |putAdvState|
operations.  In other words, we have the following type, together with three operations on it, and an interpertation
function:
\begin{code}
    data Interp as a = Interp (as -> [Bool] -> (as, [Bool], a))
    fairCoin' :: Interp as Bool
    fairCoin' as (r:rs) = (as, rs, r)
    getAdvState' :: Interp as as
    getAdvState' as rs = (as, rs, as)
    putAdvState' :: as -> Interp as ()
    putAdvState' rs as' as = (as', rs, ())

    interpret :: Game as a -> Interp as a
\end{code}

We require that |interpret| send |fairCoin|, |getAdvState|, and (for every |as'|) |putAdvState as'| to |fairCoin'|,
|getAdvState'|, and |putAdvState' as'| respectively, in the sense that the resulting functions are equal on all inputs.

In general, we do not want to require this kind of strict equality for interpretations.  For example, the following two
games should be considered equivalent, even though their interpretations are not elementwise equal:
\begin{code}
game1 :: Game () (Bool, Bool)
game1 = do
    x <- flipCoin
    y <- flipCoin
    return (x, y)

game2 :: Game () (Bool, Bool)
game2 = do
    y <- flipCoin
    x <- flipCoin
    return (x, y)
\end{code}

The condition we impose is as follows: games $A$ and $B$ have the same interpretation if for any choice of initial
adversary state, |interpret A as| and |interpret B bs| give rise to the same distributions when applied to the uniform
distribution over infinite strings of bits.

Note that |flipCoin >> flipCoin| is equivalent to |flipCoin| in this sense, even though one uses more random bits than
the other: both give rise to distributions over triples |(rs, as, a)|, where |rs|, |as|, and |a| are independent.  The
distributions on |as| and |a| clearly agree.  The distributions on |rs| also agree, since any tail of a uniformly random
bitstring will itself be uniformly random.  In fact, we shall prove that |rs| is \emph{always} independent of |(as, a)|
and always uniformly distributed, so it suffices to look at the distributions of |(as, a)|.

The choice to keep adversary state visible allows for more precise intermediate reasoning, but comes at the cost of a
small complication when considering the program as a whole: an adversary that modifies state and then guesses at random
can be distinguished from an adversary that simply guesses at random, while the two are equivalent from the point of
view of whether they can break our encryption scheme.  We thus require that when considering the game as a whole, the
adversary resets its state to the initial one as part of the last step.

We can use this to quantify the distance between two games: since they are probability distributions, we can consider
any metric on the space of probability distributions as the distance between the two.  In practice, we are typically
interested in an upper bound on the difference in the outcomes.  Since both the output type and the adversary's state
type are typically finite, we can fix an adversary's initial state and then take the sum over the absolute differences
in the probabilities of outcomes.

We refer to this distance as the adversary's \emph{advantage}.  If no adversary is specified, we mean the maximum
advantage any adversary can have for the game in question.

\section{Security Constraints}

We now have a notion of games and a notion of closeness of games, but this does not yet give us a condition for what we
consider `very close'.

One approach is to simply choose an $\epsilon$ that we consider sufficiently small and demand that no adversary have an
advantage greater than $\epsilon$.  This gives a concrete bound on the probability, which is often desirable.

The above has the downside that in the case of many schemes, adversaries can be constructed that will win the game with
arbitrarily high probability.  Nevertheless, these schemes are secure if we take an asymptotic perspective: by letting
the scheme depend on some security parameter $n$ and requiring that every adversary's advantage be bounded by a
vanishing function of $n$, we can be sure that even though the security scheme is not impossible to break, we can easily
make it harder.

We will typically take this approach, parametrising our encryption schemes over some parameter.  However, since Agda
only allows functions to be treated as black boxes, we cannot inspect that the adversary is polynomial-time.  A major
research point of this thesis will be how we can nevertheless use asymptotic analysis of adversaries.

\section{Example: One-Time Pad}

Let us now put the above together to express that the One-Time XOR Pad encryption scheme is secure against
eavesdropping, but not secure if the adversary has access to the encryption function.

We can define the encryption scheme in Haskell as follows:
\begin{code}
    generateKeyOTP :: Int -> Game as BitVector
    generateKeyOTP = uniform

    encryptOTP :: BitVector -> BitVector -> Game as BitVector
    encryptOTP key msg = return (xor key msg)
\end{code}

Here, the |Int| taken by |generateKey| is the security parameter $n$.  We require that the two messages we receive from
the adversary be exactly $n$ bits long: otherwise, the adversary could choose $0^{n+1}$ and $1^{n+1}$ as his messages,
knowing that the last bit will not change.  Similarly, given messages of different lengths, the adversary could use the
length to find what message was used.\footnote{In the formalised version in Agda, this is achieved by using a
|BitVector| type that specifies the exact length of the strings.}

\subsection{Eavesdropper Attack}

We now regard the |EAV_game| from before specialised for the OTP encryption scheme, and parametrised by the security
parameter |n|.  Our goal is to show that for any adversary |adv|, the following game is indistinguishable from a coin
flip:
\begin{code}
otp_game :: Int -> Adversary as pt ct -> Game as Bool
otp_game n adv = do
    k <- generateKeyOTP n
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- encryptOTP k (if b then m1 else m0)
    b' <- chooseOutcome adv m'
    return (b == b')
\end{code}

Note that we know that |genearteKeyOTP| and |encryptOTP| do not access the adversary's state, and so the choice of |k| is
independent of the choices of |m0|, |m1|, and |b|.  We can thus rewrite the game as follows:
\begin{code}
otp_game1 :: Int -> Adversary as pt ct -> Game as Bool
otp_game1 n adv = do
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- (if b then generateKeyOTP n >>= \k -> encryptOTP k m0
                else generateKeyOTP n >>= \k -> encryptOTP k m1)
    b' <- chooseOutcome adv m'
    return (b == b')
\end{code}

Inspecting the definitions of |generateKeyOTP| and |encryptOTP|, we see that the first generates a uniform distribution
and the second performs a XOR.  We can thus rewrite this game to be:
\begin{code}
otp_game2 :: Int -> Adversary as pt ct -> Game as Bool
otp_game2 n adv = do
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- (if b then fmap (\k -> xor k m0) (uniform n)
                else fmap (\k -> xor k m1) (uniform n))
    b' <- chooseOutcome adv m'
    return (b == b')
\end{code}

The uniform distribution over bitstrings of length |n| is invariant under XOR with another bitstring of length |n|,
since the latter is a bijective mapping, so |fmap (\k -> xor k m0) (uniform n)| can be simplified to |uniform n|.  This
gives us the following game:
\begin{code}
otp_game3 :: Int -> Adversary as pt ct -> Game as Bool
otp_game3 n adv = do
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- uniform n
    b' <- chooseOutcome adv m'
    return (b == b')
\end{code}

Since |m'| no longer depends on |b|, we can reorder the game to be:
\begin{code}
otp_game4 :: Int -> Adversary as pt ct -> Game as Bool
otp_game4 n adv = do
    (m0, m1) <- chooseMessages adv
    m' <- uniform n
    b' <- chooseOutcome adv m'
    b <- flipCoin
    return (b == b')
\end{code}

In this game, |b| is generated once |b'| is fixed; thus |b == b'| is either |b| or |not b|.  Since |fmap not flipCoin|
is indistinguishable from |flipCoin|, in both cases this is indistinguishable from |flipCoin| from the point of view of
the result.  By our assumption that the adversary reverts the state to the initial one, this computation is thus as a
whole indistinguishable from |flipCoin|.

\subsection{Chosen-Plaintext Attack}

By modifying the game slightly, we can consider the scenario where the adversary has access to the encryption function
used by the challenger.  This is known as the Chosen-Plaintext Attack.  In this case, the adversary receives not only an
encrypted message as the input to |chooseOutcome|, but also the encryption function itself.
\begin{code}
data CPA_Adversary as pt ct = CPA_Adversary
                            { chooseMessages :: Game as (pt, pt)
                            , chooseOutcome :: ct
                                            -> (pt -> Game s ct)
                                            -> Game as Bool
                            }

otp_cpa_game :: Int -> CPA_Adversary as pt ct -> Game as Bool
otp_cpa_game n adv = do
    k <- generateKeyOTP n
    (m0, m1) <- chooseMessages adv
    b <- flipCoin
    m' <- encryptOTP k (if b then m1 else m0)
    b' <- chooseOutcome adv m' (encryptOTP k)
    return (b == b')
\end{code}

Since the |encrypt| function of OTP is at once its |decrypt| function, it is clear that the adversary can simply apply
it to |m'| and return the correct value.  We can express this in code as follows, with the |Int| parameter being the
security parameter used for the game:
\begin{code}
    otp_cpa_adv_cm :: Int -> Game (BitVector, BitVector) (BitVector, BitVector)
    otp_cpa_adv_cm n = do
        m0 <- uniform n
        m1 <- uniform n
        putAdvState (m0, m1)
        return (m0, m1)

    otp_cpa_adv_co :: BitVector
                   -> (BitVector -> Game (BitVector, BitVector) BitVector)
                   -> Game (BitVector, BitVector) Bool
    otp_cpa_adv_co m' enc = do
        (m0, m1) <- getAdvState
        putAdvState (empty, empty)
        return (enc m1 == m')
\end{code}

The |putAdvState (empty, empty)| line is necessary to satisfy our guarantee that the initial adversary state is equal to
the final adversary state.  For the rest, the algorithm is straightforward.  A more interesting question, however, is
where the proof we provided for the Eavesdropper case breaks down.

The problem with that proof is that |k| is now used not only for the generation of |m'|, but is also passed (indirectly)
to |chooseOutcome|.  A certain relation holds between |m'| and |k|, and thus if we replace |encrypt k (if b then m0 else
m1)| with |uniform n| then we must also replace all later appearances of |k| with |xor m' (if b then m0 else m1)| to
maintain this relationship.  Doing this, however, brings the proof no further, since the |chooseOutcome| call still
depends on |b|, and an essential step of the proof was making the adversary commit to a |b'| before |b| was chosen.

\section{Formalised Proofs}

Writing proofs on paper is error-prone.  Let's use a proof-assistant to make sure that what we write is actually
correct.

\section{Existing Work}

The game-playing approach to cryptographic proofs is an existing and popular technique~\cite{codebasedgames}.

There are several systems for expressing cryptographic proofs in Coq or in special-purpose
languages\footnote{EasyCrypt, FCF}.  However, these all put the imperative style that cryptographic algorithms are
typically written in at the forefront, which shapes the remainder of the system.  We intend to put more focus on the
search for an elegant type-theoretic formulation of these properties, which should make the resulting proofs more
elegant.

TODO: Stochastic lambda calculus papers, dist monad papers?
